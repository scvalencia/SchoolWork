// -----------------------
// Sebastián Valencia
// UNIVERSIDAD DE LOS ANDES
// -----------------------
using gold.**
procedure main(args:String[]) begin
	//GAutomataFrame.show(createTransducer())
	testTransducer(createTransducer())
end

procedure testTransducer(M:ITransducer) begin
	var sc:java.util.Scanner(System.$in)
	while true do
		print "----------------------------"
		print "Digite la cadena de entrada:"
		string:=sc.nextLine()
		result:=M.acceptsString(string)
		print "La cadena fue "+(result?"aceptada":"rechazada")+". Resultado:"
		print M.getOutputString()
		
	end
end

function createTransducer():ITransducer begin
	Q:={'I'} ∪ (1‥7)
	Σ:={'a', 'b', 'c', '*'}
	Σ´:={'a', 'b', 'c'}
	q₀:='I'
	F:={'I'}
	return 	GDeterministicTransducer(Q,Σ,Σ´,q₀,F,δ,g,h)
end

function δ(q,σ) begin
	if q in {'I', 1, 2, 3, 4} and σ = '*' then return q
	elseif q = 'I' and σ ≠ '*' then return 1
	elseif q = 1 then
		if σ = 'a' then return 2
		elseif σ = 'b' then return 3
		elseif σ = 'c' then return 4			
	end	
	elseif q in {2, 3, 4} and σ ≠ '*' then return q + 3
	elseif q in {5, 6, 7} then return 'I'
	end
end

function g(q) begin // Respuesta en estado
	return λ
end

function h(q,σ) begin // Respuesta en transición
	if q in {'I', 1, 2, 3, 4} and σ = '*' then return λ
	elseif q = 'I' and σ ≠ '*' then
		if σ = 'b' then return 'a'
		elseif σ = 'c' then return 'b'
		elseif σ = 'a' then return 'c'			
	end
	elseif q = 1 then return λ
	elseif q in {2, 3, 4} then return σ 
	elseif q = 5 then
		if σ = '*' then return 'a'
		else return σ
		end
	elseif q = 6 then
		if σ = '*' then return 'b'
		else return σ
		end
	elseif q = 7 then
		if σ = '*' then return 'c'
		else return σ
		end
	end
end
